# docstring = short-description
#           | short-description
#               , newline
#               , newline
#               , {long-description}
#               , [sections];
#
# short-description = word, {hash | noqa | word | colon | keyword}
# long-description  = head-line, {head-line}
# head-line = indent
#               , [word | colon | indent]
#               , {word | colon | indent | keyword}
#               , [noqa]
#               , newline
#
# sections = [arguments-section]
#               , [raises-section]
#               , [yields-section | returns-section]
#           | [raises-section]
#               , [arguments-section]
#               , [yields-section | returns-section]
#
# arguments-section = section-compound
# raises-section = section-compound
# yields-section = section-simple
# returns-section = section-simple
#
# section-simple = section-head, section-simple-body
# section-compound = section-head, section-compound-body
# section-head = keyword, colon, newline
# section-simple-body =
# indent, [type], {word | colon | indent | keyword}, newline
# {indent, indent, line}
# section-compound-body = item, {item}
# item = indent, item-name, colon, item-definition
# item-name = word, [type]
# item-definition = line, {line}
# line = { word
#      | hash
#      | colon
#      | indent
#      | keyword
#      }, [noqa] newline
# type = lparen, word, {word}, rparen
#        | word, colon
#
# noqa      = noqa-head, [colon, noqa-body]
# noqa-head = hash, word
# noqa-body = word, [list]
# list      = word, {"," word}
#
# hash    = "#"
# lparen  = "("
# rparen  = ")"
# keyword = "Args"
#         | "Arguments"
#         | "Returns
#         | "Yields"
#         | "Raises"
# indent  = " "{4}
# word    = [^\ \n\:\"\t]+
# colon   = ":"
# newline = "\n"


# No non-terminal tokens should end in a newline, except at the
# docstring level.  This allows us to more explicitly control
# newlines in a consistent way.

<start>
    ::= <docstring>

<docstring>
    ::= <short-description> <sections> <newlines>

<sections>
    ::= <split> <long-description> <sections>
    | <split> <returns-section> <sections>
    | <split> <yields-section> <sections>
    | ε

<short-description>
    ::= <line>

<long-description>
    ::= <block>

<returns-section>
    ::= <returns> <colon> <newline> <block-indented>

<yields-section>
    ::= <yields> <colon> <newline> <block-indented>

<block-indented>
    ::= <paragraph-indented> <split> <block-indented>
    | <paragraph-indented>

<block>
    ::= <paragraph> <split> <block>
    | <paragraph>

<paragraph-indented>
    ::= <indented> <line> <newline> <paragraph-indented>
    | <indented> <line>

<paragraph>
    ::= <line> <newline> <paragraph>
    | <line>

<line>
    ::= <word> <line>
    | <word>
    | <indent>

<indented>
    ::= <indent> <indents>

<indents>
    ::= <indent> <indents>
    | ε

<split>
    ::= <newline> <newline> <newlines>

<newlines>
    ::= <newline> <newlines>
    | ε

<returns>
    ::= "TokenType\.RETURNS"

<yields>
    ::= "TokenType\.YIELDS"

<colon>
    ::= "TokenType\.COLON"

<lparen>
    ::= "TokenType\.LPAREN"

<rparen>
    ::= "TokenType\.RPAREN"

<word>
    ::= "TokenType\.WORD"
    | "TokenType\.RETURNS"
    | "TokenType\.COLON"
    | "TokenType\.LPAREN"
    | "TokenType\.RPAREN"

<indent>
    ::= "TokenType\.INDENT"

<newline>
    ::= "TokenType\.NEWLINE"
