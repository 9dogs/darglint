<block-indented-two>
    ::= <paragraph-indented-two> <split> <block-indented-two>
    | <paragraph-indented-two>


<block-indented>
    ::= <paragraph-indented> <split> <block-indented>
    | <paragraph-indented>

<block>
    ::= <paragraph> <split> <block>
    | <paragraph>

<paragraph-indented-two>
    ::= <indented-two> <line> <newline> <paragraph-indented-two>
    | <indented-two> <line>

<paragraph-indented>
    ::= <indented> <line> <newline> <paragraph-indented>
    | <indented> <line>

# A paragraph allows for arbitrary indentation.  No indentation is
# required, however.
<paragraph>
    ::= <indents> <line> <newline> <paragraph>
    | <indents> <line>
    | <line> <newline> <paragraph>
    | <line>

<line>
    ::= <word> <line>
    | <word>
    | <indent>

<indented-two>
    ::= <indent> <indent> <indents>

<indented>
    ::= <indent> <indents>

<indents>
    ::= <indent> <indents>
    | ε

<split>
    ::= <newline> <newline> <newlines>

<newlines>
    ::= <newline> <newlines>
    | ε


# ---- TERMINALS ----

<word>
    ::= "TokenType\.WORD"
    | <arguments>
    | <colon>
    | <hash>
    | <indent>
    | <lparen>
    | <raises>
    | <returns>
    | <rparen>
    | <yields>

<arguments>
    ::= "TokenType\.ARGUMENTS"

<colon>
    ::= "TokenType\.COLON"

<hash>
    ::= "TokenType\.HASH"

<indent>
    ::= "TokenType\.INDENT"

<lparen>
    ::= "TokenType\.LPAREN"

<newline>
    ::= "TokenType\.NEWLINE"

<raises>
    ::= "TokenType\.RAISES"

<returns>
    ::= "TokenType\.RETURNS"

<rparen>
    ::= "TokenType\.RPAREN"

<yields>
    ::= "TokenType\.YIELDS"
