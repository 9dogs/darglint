import terminals.bnf
import noqa_statement.bnf

from darglint.parse.grammar import (
    BaseGrammar,
    P,
)
from darglint.parse.identifiers import (
    ArgumentItemIdentifier,
    ArgumentTypeIdentifier,
    ExceptionItemIdentifier,
    ExceptionIdentifier,
    ShortDescriptionIdentifier,
)

Grammar: NumpyGrammar

start: <docstring>

<docstring>
    ::= <short-description>
    | 100 <short-description> <arguments-section>
    | 100 <short-description> <arguments-section> <other-arguments-section>
    | <short-description> <long-description>
    | <short-description> <long-description> <arguments-section>
    | <short-description> <long-description> <arguments-section> <other-arguments-section>
    | <short-description> <raises-section>
    | <short-description> <long-description> <raises-section>
    | <short-description> <arguments-section> <raises-section>
    | <short-description> <receives-section> <yields-section>
    | <short-description> <returns-section>
    | <short-description> <warns-section>
    | <short-description> <yields-section>

<long-description>
    ::= <line> <newlines> <long-description>
    | <line> <newlines>
    | <line>

<short-description>
    ::= @ShortDescriptionIdentifier <line>
    | @ShortDescriptionIdentifier <line> <newlines>

<arguments-section>
    ::= <arguments-header> <newlines>
    | <arguments-header> <newline> <arguments-body> <newlines>

<other-arguments-section>
    ::= <other> <arguments-header> <newlines>
    | <other> <arguments-header> <newline> <arguments-body> <newlines>

<arguments-header>
    ::= <arguments> <newline> <header>

<arguments-body>
    ::= <argument-item> <newline> <arguments-body>
    | <argument-item>

<argument-item>
    ::= @ArgumentItemIdentifier
        <ident-list> <newline>
        <block-indented>
    | @ArgumentItemIdentifier
        <ident-list> <colon> <newline>
        <block-indented>
    | @ArgumentItemIdentifier
      @ArgumentTypeIdentifier
        <ident-list> <colon> <line> <newline>
        <block-indented>

<ident-list>
    ::= <ident>
    | <ident> <ident-list>

<warns-section>
    ::= <warns-header> <newlines>
    | <warns-header> <newline> <raises-body> <newlines>

<warns-header>
    ::= <warns> <newline> <header>

<raises-section>
    ::= <raises-header> <newlines>
    | <raises-header> <newline> <raises-body> <newlines>

<raises-header>
    ::= <raises> <newline> <header>

<raises-body>
    ::= <raises-item> <raises-body>
    | <raises-item>

<raises-item>
    ::= @ExceptionItemIdentifier
        <ident> <newline>
        <block-indented>

<receives-section>
    ::= <receives-header> <newlines>
    | <receives-header> <newline> <receives-body> <newlines>

<receives-header>
    ::= <receives> <newline> <header>

<receives-body>
    ::= <receives-item> <receives-body>
    | <receives-item>

<receives-item>
    ::= <ident> <newline>
        <block-indented>
    | <ident> <colon> <line> <newline>
      <block-indented>

<yields-section>
    ::= <yields-header> <newlines>
    | <yields-header> <newline> <yields-body> <newlines>

<yields-header>
    ::= <yields> <newline> <header>

<yields-body>
    ::= <block-indented>
    | <line> <newline> <block-indented>
    | <yields-body-typed>

<yields-body-typed>
    ::= <yields-argument> <block-indented> <newline> <yields-body-typed>
    | <yields-argument> <block-indented> <newlines>

<yields-argument>
    ::= <ident> <colon> <line> <newline>
    | <ident> <newline>

<returns-section>
    ::= <returns-header> <newlines>
    | <returns-header> <newline> <returns-body> <newlines>

<returns-header>
    ::= <returns> <newline> <header>

# TODO: Change the yields body to match this.
<returns-body>
    ::= <block-indented>
    | <returns-body-typed>

<returns-body-typed>
    ::= <returns-argument> <block-indented> <newline> <returns-body-typed>
    | <returns-argument> <block-indented> <newlines>

<returns-argument>
    ::= <ident> <colon> <line> <newline>
    | <ident> <newline>

<block-indented>
    ::= <paragraph-indented> <split> <block-indented>
    | <paragraph-indented>

<paragraph-indented>
    ::= <indented> <line> <newline> <paragraph-indented>
    | <indented> <line>

<indented>
    ::= <indent> <indents>

<indents>
    ::= <indent> <indents>
    | ε

<split>
    ::= <newline> <newline> <newlines>

<newlines>
    ::= <newline> <newlines>
    | <newline>
    | ε

<line>
    ::= <word> <line>
    | <word> <noqa-maybe>
    | <noqa-statement>
